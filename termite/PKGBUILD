# $Id$
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgbase=termite
pkgname=('termite-ndreas')
pkgver=13
pkgrel=2
pkgdesc="A simple VTE-based terminal"
url="https://github.com/thestinger/termite/"
license=('LGPL')
arch=('x86_64')
depends=('gtk3' 'pcre2' 'gnutls' 'vte-common')
makedepends=('git' 'ncurses' 'intltool' 'gperf' 'gtk-doc' 'gobject-introspection')
_vtever=0.50.2.a
source=("git+https://github.com/thestinger/termite#tag=v$pkgver"
        "git+https://github.com/thestinger/vte-ng#tag=$_vtever"
        "termite-util::git+https://github.com/thestinger/util"
        "window-class.patch")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'beb40619ccf050b94e368336e9b325afe8d8627f7e3ef143b589516ed6c5632e')

prepare() {
  cd vte-ng
  echo 'sources: $(BUILT_SOURCES)' >> src/Makefile.am
  NOCONFIGURE=1 ./autogen.sh

  cd ../$pkgbase
  git submodule init
  git config --local submodule.util.url "$srcdir/termite-util"
  git submodule update

  patch -Np1 -i "${srcdir}/window-class.patch" "${srcdir}/termite/termite.cc"
}

build() {
  cd vte-ng
  ./configure \
    --prefix="$srcdir/vte-static" \
    --localedir="/usr/share/$pkgbase/locale" \
    --enable-static \
    --disable-shared \
    enable_introspection=no \
    enable_vala=no \
    --disable-gtk-doc \
    --disable-glade-catalogue
  make -C src sources install-exec install-data
  make install-pkgconfigDATA

  cd ../$pkgbase
  export PKG_CONFIG_PATH="$srcdir/vte-static/lib/pkgconfig"
  make
}

package_termite-ndreas() {
  depends+=('termite-terminfo')
  conflicts=('termite')
  backup=(etc/xdg/termite/config)

  make -C vte-ng/po DESTDIR="$pkgdir" install-data
  make -C $pkgbase DESTDIR="$pkgdir" PREFIX=/usr install
  rm -r "$pkgdir/usr/share/terminfo"
}
